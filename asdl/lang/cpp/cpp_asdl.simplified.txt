## ASDL's 4 builtin types are
identifier, int, string, constant

cu = TranslationUnit(statement* stmts)

declaration = AccessSpecDecl(identifier access_spec)
            | CXXConstructorDecl(identifier name, constant? noexcept, default_mode? defaulted, statement? body, parameter* parameters, cxx_ctor_initializer* initializers)
            | CXXConversionDecl(identifier name, statement* subnodes)
            | CXXDestructorDecl(identifier name, constant? noexcept, constant? virtual, default_mode? defaulted, statement? body)
            | CXXMethodDecl(identifier name, type return_type, constant? virtual, constant? noexcept, constant? const, default_mode? defaulted, final? final, constant? ref_qualifier, statement? body, parameter* parameters)
            | CXXRecordDecl(identifier name, identifier kind, identifier bases, constant complete_definition, declaration* subnodes)
            | ClassTemplateDecl(statement* subnodes)
            | EmptyDecl
            | EnumDecl(identifier? name, enum_field* fields)
            | FieldDecl(identifier name, type type, expression? init, attribute* attributes)
            | FriendDecl(identifier type)
            | FunctionDecl(identifier name, type return_type, constant? storage, constant? variadic, constant? inline, exception_spec? exception, statement? body, parameter* parameters)
            | FunctionTemplateDecl(statement* subnodes)
            | NamespaceDecl(identifier name, statement* subnodes)
            | NonTypeTemplateParmDecl(identifier name, identifier type, statement* subnodes)
            | TemplateTypeParmDecl(identifier name, statement* subnodes)
            | TypedefDecl(identifier name, type type)
            | UsingDirectiveDecl(identifier name)
            | VarDecl(identifier name, type type, constant? storage_class, constant init_mode, constant? implicit, constant? referenced, expression? init, attribute* attributes, constant? tls)

statement = BreakStmt
          | CXXForRangeStmt(statement* subnodes)
          | CaseStmt(expression pattern, statement stmt)
          | CompoundStmt(statement* stmts)
          | ContinueStmt
          | DeclStmt(declaration* decls)
          | DefaultStmt(statement stmt)
          | DoStmt(expression cond, statement body)
          | ExprStmt(expression expr)
          | ExprWithCleanups(statement* subnodes)
          | ForStmt(declaration_or_expression? init, declaration_or_expression? cond, expression? inc, statement body)
          | FullComment(string comment)
          | GCCAsmStmt(constant string, constrained_expression* output_operands, constrained_expression* input_operands, constant* clobbers, constant* labels)
          | GotoStmt(identifier target)
          | IfStmt(declaration_or_expression cond, statement true_body, statement? false_body)
          | LabelStmt(identifier name, statement stmt)
          | NullStmt
          | OverrideAttr
          | ReturnStmt(expression? value)
          | SwitchStmt(expression cond, statement body)
          | TypeRef(identifier name)
          | WhileStmt(declaration_or_expression cond, statement body)

expression = ArraySubscriptExpr(expression base, expression index)
           | BinaryOperator(constant opcode, expression lhs, expression rhs)
           | CStyleCastExpr(type type, expression expr)
           | CXXBindTemporaryExpr(expression* subnodes)
           | CXXBoolLiteralExpr(constant value)
           | CXXConstructExpr(identifier type, expression* subnodes)
           | CXXDefaultArgExpr
           | CXXFunctionalCastExpr(identifier type, expression* subnodes)
           | CXXMemberCallExpr(expression* subnodes)
           | CXXNewExpr(type type, expression* args, constant? is_array)
           | CXXNullPtrLiteralExpr
           | CXXOperatorCallExpr(expression left, expression op, expression right)
           | CXXStdInitializerListExpr(expression* subnodes)
           | CXXTemporaryObjectExpr(identifier type, expression* subnodes)
           | CXXThisExpr
           | CallExpr(expression callee, expression* args)
           | CharacterLiteral(identifier value)
           | CompoundAssignOperator(constant opcode, expression lhs, expression rhs)
           | ConditionalOperator(expression cond, expression true_expr, expression false_expr)
           | ConstantExpr(identifier value, expression expr)
           | DeclRefExpr(identifier name, identifier kind)
           | FloatingLiteral(type type, identifier value)
           | ImplicitCastExpr(type type, expression expr)
           | ImplicitValueInitExpr
           | InitListExpr(expression* values)
           | IntegerLiteral(type type, identifier value)
           | MaterializeTemporaryExpr(expression expr)
           | MemberExpr(identifier name, identifier op, expression? expr)
           | ParenExpr(expression expr)
           | StringLiteral(identifier value)
           | UnaryExprOrTypeTraitExpr(identifier name, expression? expr, type? type)
           | UnaryOperator(constant opcode, constant postfix, expression expr)

parameter = ParmVarDecl(type type, identifier? name, expression? default)

declaration_or_expression = DeclOrExpr(declaration* decl, expression? expr)

cxx_ctor_initializer = CXXCtorInitializer(identifier name, expression* args)


type = BuiltinType(identifier name)
     | ConstantArrayType(type type, constant size)
     | DecayedType(type type)
     | ElaboratedType(type type, identifier? qualifiers)
     | EnumType(identifier name)
     | FunctionProtoType(type return_type, type* parameter_types)
     | IncompleteArrayType(type type)
     | LValueReferenceType(type type)
     | ParenType(type type)
     | PointerType(type type)
     | QualType(identifier? qualifiers, type type)
     | RValueReferenceType(type type)
     | RecordType(identifier name)
     | VectorType(type type, constant size)

enum_field = EnumConstantDecl(identifier name, expression? init)

constrained_expression = ConstrainedExpression(expression expr, constant constraint)

attribute = AlignedAttr(expression? size)
          | AliasAttr(identifier aliasee)
          | CleanupAttr(identifier func)
          | DeprecatedAttr(constant? msg)
          | PackedAttr
          | RetainAttr
          | SectionAttr(constant section)
          | TLSModelAttr(constant tls_model)
          | UnavailableAttr(constant? msg)
          | UninitializedAttr
          | UnusedAttr
          | UsedAttr
          | VisibilityAttr(constant visibility)
          | WeakAttr

default_mode = Default
             | Delete
             | PureVirtual

final = FinalAttr

exception_spec = Throw(identifier* args)
               | NoExcept(constant? repr)
